plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.openapi.generator' version '7.5.0'
}

group = 'com.mayra0419'
version = '0.0.1-SNAPSHOT'

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/swagger.yml"
	outputDir = "$buildDir/generated"
	apiPackage = "com.mayra0419.usersmanager.swagger"
	modelPackage = "com.mayra0419.usersmanager.swagger.model"
	configOptions = [
			library: "spring-boot",
			interfaceOnly: "true",
			serviceInterface: "false",
			useSpringBoot3: "true",
			documentationProvider: "none",
			useBeanValidation: "false",
			openApiNullable: "false"
	]
}

sourceSets {
	main {
		java.srcDirs += "$buildDir/generated/src/main/java"
	}
}

tasks {
	compileJava {
		dependsOn("openApiGenerate")
	}
}

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.security:spring-security-core'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.12.5'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.12.5'
	compileOnly "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
